{
	Directory: {
		directory_id: string,
		name: string,
		parent_directory_id?: string
	},

	File: {
		file_id: string,
		name: string,
		format?: "mp4" | "jpeg" | "vtt" | "json",
		parent_directory_id?: string,
		index_timestamp?: number
	},

	AudioResource: {
		type: "audio",
		duration_ms: number
	},

	DataResource: {
		type: "data"
	},

	ImageResource: {
		type: "image",
		width: number,
		height: number
	},

	SubtitleResource: {
		type: "subtitle",
		duration_ms: number,
		language?: string
	},

	VideoResource: {
		type: "video",
		duration_ms: number,
		width: number,
		height: number
	},

	Resource: AudioResource | DataResource | ImageResource | SubtitleResource | VideoResource,

	FileAudioResource: AudioResource & {
		resource_id: string,
		file_id: string,
		index: number
	},

	FileDataResource: DataResource & {
		resource_id: string,
		file_id: string,
		index: number
	},

	FileImageResource: ImageResource & {
		resource_id: string,
		file_id: string,
		index: number
	},

	FileSubtitleResource: SubtitleResource & {
		resource_id: string,
		file_id: string,
		index: number
	},

	FileVideoResource: VideoResource & {
		resource_id: string,
		file_id: string,
		index: number
	},

	FileResource: FileAudioResource | FileDataResource | FileImageResource | FileSubtitleResource | FileVideoResource,

	Artist: {
		artist_id: string,
		name: string
	},

	Album: {
		album_id: string,
		title: string,
		year?: number
	},

	AlbumFile: {
		album_id: string,
		file_id: string
	},

	Disc: {
		disc_id: string,
		album_id: string,
		number: number
	},

	Track: {
		track_id: string,
		disc_id: string,
		title: string,
		number: number
	},

	TrackFile: {
		track_id: string,
		file_id: string
	},

	AlbumArtist: {
		album_id: string,
		artist_id: string,
		order: number
	},

	TrackArtist: {
		track_id: string,
		artist_id: string,
		order: number
	},

	Show: {
		show_id: string,
		name: string,
		summary?: string
	},

	ShowFile: {
		show_id: string,
		file_id: string
	},

	Season: {
		season_id: string,
		show_id: string,
		number: number
	},

	Episode: {
		episode_id: string,
		season_id: string,
		title: string,
		number: number,
		year?: number,
		summary?: string
	},

	EpisodeFile: {
		episode_id: string,
		file_id: string
	},

	Movie: {
		movie_id: string,
		title: string,
		year?: number,
		summary?: string
	},

	MovieFile: {
		movie_id: string,
		file_id: string
	},

	Person: {
		person_id: string,
		name: string
	},

	MoviePerson: {
		movie_id: string,
		person_id: string,
		order: number
	},

	ShowPerson: {
		show_id: string,
		person_id: string,
		order: number
	},

	Genre: {
		genre_id: string,
		name: string
	},

	MovieGenre: {
		movie_id: string,
		genre_id: string,
		order: number
	},

	ShowGenre: {
		show_id: string,
		genre_id: string,
		order: number
	},

	Cue: {
		cue_id: string,
		resource_id: string,
		start_ms: number,
		duration_ms: number,
		lines: string[]
	},

	User: {
		user_id: string,
		name: string,
		username: string,
		password: string
	},

	Token: {
		token_id: string,
		user_id: string,
		validator: string,
		expires_ms: number
	},

	Stream: {
		stream_id: string,
		user_id: string,
		file_id: string,
		timestamp_ms: number
	},

	Playlist: {
		playlist_id: string,
		title: string,
		description: string,
		user_id: string
	},

	PlaylistItem: {
		playlist_item_id: string,
		playlist_id: string,
		track_id: string,
		number: number
	}
}
